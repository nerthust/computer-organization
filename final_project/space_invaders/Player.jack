class Player {
    field int x, y;
    field int width, height;
    field int direction;
    constructor Player new(int Ax, int Ay, int Awidth, int Aheight) {
        let x = Ax;
        let y = Ay;
        let width = Awidth;
        let height = Aheight;
        let direction = 0;
        do show();
        return this;
    }

    method void dispose() {
        do Memory.deAlloc(this);
        return;
    }

    method void show() {
        do Screen.setColor(true);
	    do draw();
        return;
    }

    method int get_x() {
        return x;
    }

    method void draw() {
        do Screen.drawRectangle(x, y, x + width, y + height);
        return;
    }

    method int get_direction() {
        return direction;
    }

    method void setDirection(int Adirection) {
        let direction = Adirection;
        return;
    }

    method void move() {
        if (direction = 1) {
            let x = x - 4;
	        if (x < 0) {
		        let x = 0;
    	    }
            do Screen.setColor(false);
            do Screen.drawRectangle((x + width) + 1, y, (x + width) + 4, y + height);
            do Screen.setColor(true);
   	        do Screen.drawRectangle(x, y, x + 3, y + height);
	    } 
	    else {
            if (direction = 2) {
                let x = x + 4;
                if ((x + width) > 511) {
                    let x = 511 - width;
                }
                do Screen.setColor(false);
                do Screen.drawRectangle(x - 4, y, x - 1, y + height);
                do Screen.setColor(true);
                do Screen.drawRectangle((x + width) - 3, y, x + width, y + height);
            }
	    }
        do Sys.wait(20);
        return;
    }
}